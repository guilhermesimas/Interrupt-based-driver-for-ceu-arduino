#include "arduino/arduino.ceu"

// Change color button
input int PIN_08;

// RED / BLUE / GREEN Terminals of RGB LED
output int PIN_05;
output int PIN_06;
output int PIN_07;


// Event marking a color change
event void change_color;

event void turn_led_on;
event void turn_led_off;


// Startup routine to check the LEDs
emit PIN_05( HIGH );
await 500ms;
emit PIN_05( LOW );
await 500ms;
emit PIN_06( HIGH );
await 500ms;
emit PIN_06( LOW );
await 500ms;
emit PIN_07( HIGH );
await 500ms;
emit PIN_07( LOW );
await 500ms;

var int pin = 5;

par do
	loop do
		await 500ms;
		emit turn_led_on;
		await 500ms;
		emit turn_led_off;
	end
with
	loop do
		await turn_led_on;
		_digitalWrite(pin,HIGH);
	end
with
	loop do
		await turn_led_off;
		_digitalWrite(5,LOW);
		_digitalWrite(6,LOW);
		_digitalWrite(7,LOW);
	end
with
	loop do
		await PIN_08 ;
		emit change_color;
		await 50us;
		await PIN_08 ;
		await 50us;
	end
with
	loop do
		pin = 5;
		await change_color ;
		pin = 6;
		await change_color ;
		pin = 7;
		await change_color ;
	end
with
	loop do
		await change_color;
		if _digitalRead(5)==HIGH or _digitalRead(6)==HIGH or _digitalRead(7)==HIGH then
			emit turn_led_off;
			emit turn_led_on;
		end
	end
end
